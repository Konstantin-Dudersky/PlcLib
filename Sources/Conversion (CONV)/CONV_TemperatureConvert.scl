FUNCTION "CONV_TemperatureConvert" : Real
{ S7_Optimized_Access := 'TRUE' }
   VAR_INPUT 
      mode : Int;   // 1: Celsius in Fahrenheit, 2: Fahrenheit in Celsius, 3: Celsius in Kelvin, 4: Kelvin in Celsius, 5: Fahrenheit in Kelvin, 6: Kelvin in Fahrenheit
      temperature : Real;
   END_VAR

   VAR_OUTPUT 
      error : Bool;
      status : Word;
   END_VAR

   VAR CONSTANT 
      NO_ERROR : Word := 16#0000;
      NO_CURRENT_JOBS : Word := 16#7000;
      NO_MODE : Word := 16#8200;
   END_VAR


BEGIN
	//=============================================================================
	
	//-----------------------------------------------------------------------------
	// Library:       LGF (Library General Functions)
	// Tested with:   CPU1212C DC/DC/DC FW:V4.2
	// Engineering:   TIA Portal V14 Update 1
	// Restrictions:  -
	// Requirements:  PLC (S7-1200 / S7-1500)
	// Functionality: This function converts the temperature in another unit
	//-----------------------------------------------------------------------------
	// Change log table:
	// Version  Date        In charge / Changes applied
	// 01.00.00 19.08.2015  Siemens Industry Online Support
	//                      First released version
	// 01.00.01 02.01.2017  Siemens Industry Online Support
	//                      Upgrade: TIA Portal V14 Update 1
	// 01.00.02 17.08.2018  Siemens Industry Online Support
	//                      Upgrade: TIA V15 Update 2
	// 01.00.03 23.11.2018  Siemens Industry Online Support
	//                      Upgrade: TIA V15.1
	//=============================================================================
	
	//set "No current job" status
	#error := false;
	#status := #NO_CURRENT_JOBS;
	
	CASE #mode OF
	    1:  // Celsius in Fahrenheit
	       #CONV_TemperatureConvert := #temperature * 1.8 + 32.0; //F = C * 1.8 + 32
	    2:  // Fahrenheit in Celsius
	       #CONV_TemperatureConvert := (#temperature - 32.0)/ 1.8; //C = (F - 32) / 1.8
	    3:  // Celsius in Kelvin
	       #CONV_TemperatureConvert := #temperature + 273.15; //K = C + 273.15
	    4:  // Kelvin in Celsius
	       #CONV_TemperatureConvert := #temperature - 273.15; //C = K - 273.15
	    5:  // Fahrenheit in Kelvin
	       #CONV_TemperatureConvert := ((#temperature - 32.0) / 1.8) + 273.15 ; //K := ((C - 32.0) / 1.8) + 273.15
	    6:  // Kelvin in Fahrenheit
	       #CONV_TemperatureConvert := (#temperature - 273.15) * 1.8 + 32.0; //F := (K - 273.15) * 1.8 + 32.0
	    ELSE  // no mode selected
	      #error := true;
	      #status:= #NO_MODE;
	      #CONV_TemperatureConvert := 0.0;
	      RETURN;
	  END_CASE;
	  
	  #status := #NO_ERROR;
END_FUNCTION

